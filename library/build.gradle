apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "1"
        version="1"
        archivesBaseName="paypoint_sdk"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            libraryVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.aar')) {
                        def fileName = "${archivesBaseName}.aar"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }



    packagingOptions {
        exclude 'LICENSE.txt'
    }

    lintOptions {
        abortOnError false
    }
}

android.libraryVariants.all { variant ->

    task("${variant.name}Javadoc", type: Javadoc) {
        title = "PayPoint SDK $version API"
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar =
                "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
        options.links("http://docs.oracle.com/javase/7/docs/api/");
        options.links("http://d.android.com/reference/");
        options.showFromPublic()
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
        exclude '**/log'
        exclude '**/utils'
        exclude '**/device'
        exclude '**/ThreeDSActivity.java'
        exclude '**/ThreeDSResumeRequest.java'
        exclude '**/MakePaymentRequest.java'
        exclude '**/MakePaymentResponse.java'
        exclude '**/PaymentMethod.java'
        exclude '**/DeviceInfo.java'
        exclude '**/NetworkManager.java'
        exclude '**/PayPointService.java'
        exclude '**/SelfSignedSocketFactory.java'
    }
}

uploadArchives {

    configuration = configurations.archives
    repositories.mavenDeployer {

        repository(url: project.property('paypoint.maven.repo')) {
            authentication(userName: project.property('paypoint.maven.repo.username'),
                    password: project.property('paypoint.maven.repo.password'))
        }

        pom.project {
            pom.groupId = 'net.paypoint'
            pom.artifactId = "mobilesdk-android"
            pom.version = project.property('paypoint.maven.pom.version')
            name 'PayPoint Advanced Payments SDK'
            packaging 'aar'
            description 'Library for making mobile payments with PayPoint'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.netflix.rxjava:rxjava-android:0.20.7'
}
