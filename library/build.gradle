apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "1.0.0"
        archivesBaseName="pay360_sdk"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            libraryVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.aar')) {
                        def fileName = "${archivesBaseName}.aar"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }



    packagingOptions {
        exclude 'LICENSE.txt'
    }

    lintOptions {
        abortOnError false
    }
}

android.libraryVariants.all { variant ->

    task("${variant.name}Javadoc", type: Javadoc) {
        title = "Pay360 SDK $version API"
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar =
                "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
        options.links("http://docs.oracle.com/javase/7/docs/api/");
        options.links("http://d.android.com/reference/");
        options.showFromPublic()
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
        exclude '**/log'
        exclude '**/utils'
        exclude '**/device'
        exclude '**/ThreeDSActivity.java'
        exclude '**/ThreeDSResumeRequest.java'
        exclude '**/MakePaymentRequest.java'
        exclude '**/MakePaymentResponse.java'
        exclude '**/PaymentMethod.java'
        exclude '**/DeviceInfo.java'
        exclude '**/NetworkManager.java'
        exclude '**/Pay360Service.java'
        exclude '**/SelfSignedSocketFactory.java'
    }
}

uploadArchives {

    configuration = configurations.archives
    repositories.mavenDeployer {

        if (project.hasProperty('pay360.maven.repo')) {
            repository(url: project.property('pay360.maven.repo')) {

                if (project.hasProperty('pay360.maven.repo.username') &&
                    project.hasProperty('pay360.maven.repo.password')) {
                    authentication(userName: project.property('pay360.maven.repo.username'),
                            password: project.property('pay360.maven.repo.password'))
                }
            }

            pom.project {
                pom.groupId = 'com.pay360'
                pom.artifactId = "mobilesdk-android"
                pom.version = project.property('pay360.maven.pom.version')
                name 'Pay360 Advanced Payments SDK'
                packaging 'aar'
                description 'Library for making mobile payments with Pay360'
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.netflix.rxjava:rxjava-android:0.20.7'
}
